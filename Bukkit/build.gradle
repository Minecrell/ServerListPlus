/*
 *        _____                     __    _     _   _____ _
 *       |   __|___ ___ _ _ ___ ___|  |  |_|___| |_|  _  | |_ _ ___
 *       |__   | -_|  _| | | -_|  _|  |__| |_ -|  _|   __| | | |_ -|
 *       |_____|___|_|  \_/|___|_| |_____|_|___|_| |__|  |_|___|___|
 *
 *  ServerListPlus - http://git.io/slp
 *  Copyright (c) 2014, Minecrell <https://github.com/Minecrell>
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

repositories {
    maven {
        name = 'sonatype-snapshots'
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        name = 'paper'
        url = 'https://destroystokyo.com/repo/repository/maven-snapshots'
    }
    maven {
        name = 'dmulloy2'
        url = 'http://repo.dmulloy2.net/nexus/repository/releases'
    }
    maven {
        name = 'dmulloy2-snapshots'
        url = 'http://repo.dmulloy2.net/nexus/repository/snapshots'
    }
    maven {
        name = 'minecrell-snapshots'
        url = 'https://repo.minecrell.net/snapshots'
    }
    maven {
        name = 'confuser-repo'
        url = 'http://ci.frostcast.net/plugin/repository/everything'
    }
}

ext {
    // TODO: Better fix?
    resourceTokens = resourceTokens.clone()
    pluginPackage = "${javaPackage}.bukkit"
    pluginClass = "${pluginPackage}.BukkitPlugin"
    resourceTokens.PluginClass = pluginClass.toString()
}

dependencies {
    compile('com.destroystokyo.paper:paper-api:1.13-R0.1-SNAPSHOT') {
        exclude(group: 'com.mojang', module: 'authlib') // TODO: Remove in 1.13
    }
    compile 'com.comphenix.protocol:ProtocolLib-API:4.2.1'
    compile('org.mcstats.bukkit:metrics-lite:R8-SNAPSHOT') {
        transitive = false
    }
    
    compileOnly('com.github.netherfoam:MaxBans:156239e1f1') {
        transitive = false
    }
    compileOnly 'me.confuser:BanManager:5.10.0'
}

processResources {
    include 'plugin.yml'
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: resourceTokens
}

shadowJar {
    dependencies {
        include dependency('org.mcstats.bukkit:metrics-lite')
    }

    relocate 'org.mcstats', "${pluginPackage}.metrics"
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

task v1_7_X(type: ShadowJar) {
    classifier = '1.7.X'
    relocate 'com.google.common', 'net.minecraft.util.com.google.common'
    relocate 'com.google.gson', 'net.minecraft.util.com.google.gson'
}

task v1_8(type: ShadowJar) {
    classifier = '1.8'
    relocate 'com.google.gson', 'org.bukkit.craftbukkit.libs.com.google.gson'
}

configure([v1_7_X, v1_8]) {
    baseName = rootProject.name
    classifier = "$project.name-$classifier"

    from sourceSets.main.output
    exclude 'META-INF/'
    configurations = [project.configurations.runtime]

    dependencies {
        include project(':')
        include dependency('org.mcstats.bukkit:metrics-lite')
    }

    relocate 'org.mcstats', "${pluginPackage}.metrics"
}

artifacts {
    archives v1_7_X
    archives v1_8
}
